plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
    id 'io.freefair.lombok' version '8.13.1'
}

group 'com.github.hmcts'
version System.getenv('RELEASE_VERSION') ?: "DEV-SNAPSHOT-${new Date().format('yyyyMMdd-HHmmss')}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '11.0.0'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'et-data-model'
            version project.version
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

ext {
    lombokVersion = '1.18.38'
}

dependencies {
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.hibernate.validator:hibernate-validator:6.2.5.Final'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.19.2'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
    implementation group: 'org.springframework', name: 'spring-core', version: '6.2.9'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.19.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
}

jar {
    enabled = true
}

test {
    useJUnitPlatform()
}

tasks.register('printVersion') {
    doLast {
        println "Publishing version: ${project.version}"
    }
}

publishToMavenLocal.dependsOn printVersion
